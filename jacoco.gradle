apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.9"
}

project.afterEvaluate {
    // Grab all build types and product flavors
    def buildTypes = android.buildTypes.collect { type ->
        type.name
    }
    def productFlavors = android.productFlavors.collect { flavor ->
        flavor.name
    }
    // When no product flavors defined, use empty
    if (!productFlavors) productFlavors.add('')
    productFlavors.each { productFlavorName ->
        buildTypes.each { buildTypeName ->
            def sourceName, sourcePath
            if (!productFlavorName) {
                sourceName = sourcePath = "${buildTypeName}"
            } else {
                sourceName = "${productFlavorName}${buildTypeName.capitalize()}"
                sourcePath = "${productFlavorName}/${buildTypeName}"
            }
            def testTaskName = "test${sourceName.capitalize()}UnitTest"
            // Create coverage task of form 'testFlavorTypeCoverage' depending on 'testFlavorTypeUnitTest'
            task "${testTaskName}Coverage"(type: JacocoReport, dependsOn: "$testTaskName") {
                group = "Reporting"
                description = "Generate Jacoco coverage reports on the ${sourceName.capitalize()} build."
                classDirectories = fileTree(
                        dir: "${project.buildDir}/intermediates/classes/${sourcePath}",
                        excludes: [
                                '**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                                '**/*_ViewBinding*.*',
                                '**/*$Lambda$*.*',
                                '**/*Module.*', '**/*Dagger*.*',
                                '**/*MembersInjector*.*', '**/*_Provide*Factory*.*', '**/*_Factory.*',
                                '**/di/*',
                                '**/Lambda$*.class',
                                '**/Lambda.class',
                                '**/*Lambda.class',
                                '**/*Lambda*.class',
                                '**/*$PresentersBinder$*.class', '**/*$PresentersBinder.class',
                                '**/*$ViewStateProvider.class', '**/*$State$*.class', '**/*$State.class',
                                '**/*StethoHelper*.*',
                                '**/*Unit*.*',
                                '**/*Dao*.*', '**/*AppDatabase*.*',
                                '**/entity/**', '**/response/**',
                                'com/arellomobile/mvp/**',
                                '**/*Adapter*.*', '**/*DiffCallBack*.*', '**/*ItemDecoration*.*',
                                '**/*ConverterValue.class'
                        ]
                )
                def coverageSourceDirs = [
                        "src/main/java",
                        "src/$productFlavorName/java",
                        "src/$buildTypeName/java"
                ]
                additionalSourceDirs = files(coverageSourceDirs)
                sourceDirectories = files(coverageSourceDirs)
                executionData = files("${project.buildDir}/jacoco/${testTaskName}.exec")
                reports {
                    xml.enabled = true
                    html.enabled = true
                }

                outputs.upToDateWhen { false }
            }
        }
    }
}